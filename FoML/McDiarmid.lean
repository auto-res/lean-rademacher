import FoML.ExpectationInequalities
import FoML.Hoeffding
import FoML.MeasurePiLemmas
import Mathlib.Tactic.Cases

open MeasureTheory ProbabilityTheory

lemma double_integral_indep_eq_integral {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {Î© : Type*} {Î² : Type*} {Î²' : Type*}
  {_mÎ© : MeasurableSpace Î©} {Î¼ : Measure Î©} {f : Î© â†’ Î²} {g : Î© â†’ Î²'}
  {F : Î² Ã— Î²' â†’ E}
  {mÎ² : MeasurableSpace Î²} {mÎ²' : MeasurableSpace Î²'} [MeasureTheory.IsFiniteMeasure Î¼]
  {hF' : StronglyMeasurable F}
  (hf : AEMeasurable f Î¼) (hg : AEMeasurable g Î¼) (hfg : ProbabilityTheory.IndepFun f g Î¼)
  (hF: HasFiniteIntegral F ((Measure.map f Î¼).prod (Measure.map g Î¼))):
  âˆ« (Ï‰ : Î©), (âˆ« (Ï‰' : Î©), F (f Ï‰, g Ï‰') âˆ‚Î¼) âˆ‚Î¼ = âˆ« (Ï‰ : Î©), F (f Ï‰, g Ï‰) âˆ‚Î¼ := by
  let Î½ : Measure Î² := Measure.map f Î¼
  let Î½' : Measure Î²' := Measure.map g Î¼
  have hfg' : Measure.map (fun (Ï‰ : Î©) => (f Ï‰, g Ï‰)) Î¼ = Î½.prod Î½' := by
    exact (ProbabilityTheory.indepFun_iff_map_prod_eq_prod_map_map hf hg).mp hfg
  calc
  _ = âˆ« (Ï‰ : Î©), (âˆ« (x' : Î²'), F (f Ï‰, x') âˆ‚ Î½') âˆ‚Î¼ := by
    apply integral_congr_ae
    filter_upwards with Ï‰
    apply Eq.symm
    apply MeasureTheory.integral_map
    assumption
    apply StronglyMeasurable.aestronglyMeasurable
    apply hF'.comp_measurable
    exact measurable_prodMk_left
  _ = âˆ« (x : Î²), (âˆ« (x' : Î²'), F (x, x') âˆ‚ Î½') âˆ‚ Î½ := by
    apply Eq.symm
    apply MeasureTheory.integral_map
    assumption
    apply StronglyMeasurable.aestronglyMeasurable
    exact StronglyMeasurable.integral_prod_right hF'
  _ = âˆ« (z : Î² Ã— Î²'), F z âˆ‚ (Î½.prod Î½') := by
    apply Eq.symm
    apply MeasureTheory.integral_prod
    exact âŸ¨hF'.aestronglyMeasurable, hFâŸ©
  _ = âˆ« (z : Î² Ã— Î²'), F z âˆ‚ (Measure.map (fun (Ï‰ : Î©) => (f Ï‰, g Ï‰)) Î¼) := by rw [â†hfg']
  _ = âˆ« (Ï‰ : Î©), F (f Ï‰, g Ï‰) âˆ‚Î¼ := by
    apply MeasureTheory.integral_map
    exact AEMeasurable.prodMk hf hg
    rw [hfg']
    exact hF'.aestronglyMeasurable

theorem ProbabilityTheory.iIndepFun.comp_right
  {Î© Î¹ Î¹' : Type*} [DecidableEq Î¹] {_mÎ© : MeasurableSpace Î©}
  {Î¼ : MeasureTheory.Measure Î©} {Î² : Î¹ â†’ Type*}
  {mÎ² : (i : Î¹) â†’ MeasurableSpace (Î² i)}
  {f : (i : Î¹) â†’ Î© â†’ Î² i} (h : ProbabilityTheory.iIndepFun f Î¼)
  {g : Î¹' â†’ Î¹} (hg : Function.Injective g):
  ProbabilityTheory.iIndepFun (fun i â†¦ f (g i)) Î¼ := by
  simp only [iIndepFun, Kernel.iIndepFun, Kernel.iIndep, Kernel.iIndepSets, Set.mem_setOf_eq,
    Kernel.const_apply, ae_dirac_eq, Filter.eventually_pure] at *
  intro s' fâ‚' hâ‚'
  let s := s'.image g
  have hunique (i : Î¹): âˆ€ (yâ‚ yâ‚‚ : Î¹'), yâ‚ âˆˆ s' âˆ§ g yâ‚ = i â†’ yâ‚‚ âˆˆ s' âˆ§ g yâ‚‚ = i â†’ yâ‚ = yâ‚‚ := by
    intro yâ‚ yâ‚‚ âŸ¨_, hyâ‚âŸ© âŸ¨_, hyâ‚‚âŸ©
    exact hg <| hyâ‚.trans hyâ‚‚.symm
  let invg : Î  (i : s), { i' : Î¹' // i' âˆˆ s' âˆ§ g i' = i.1 } := fun âŸ¨i, hiâŸ© â†¦ by
    simp only [Finset.mem_image, s] at hi
    exact Finset.chooseX (fun i' â†¦ g i' = i) s' <| existsUnique_of_exists_of_unique hi (hunique i)
  let fâ‚ : Î¹ â†’ Set Î© := fun i â†¦
    if hi : i âˆˆ s then
      fâ‚' (invg âŸ¨i,hiâŸ©).1
    else
      Set.univ
  have hâ‚ : âˆ€ i âˆˆ s, @MeasurableSet Î© (MeasurableSpace.comap (f i) (mÎ² i)) (fâ‚ i) := by
    intro i hi
    dsimp only [fâ‚]
    rw [dif_pos hi]
    have := hâ‚' (invg âŸ¨i,hiâŸ©).1 (invg âŸ¨i,hiâŸ©).2.1
    rw [(invg âŸ¨i, hiâŸ©).2.2] at this
    exact this

  calc
    _ = Î¼ (â‹‚ i âˆˆ s, fâ‚ i) := by
      apply congrArg
      apply HasSubset.Subset.antisymm
      Â· intro x hx
        apply Set.mem_iInterâ‚‚_of_mem
        intro i hi
        dsimp only [fâ‚]
        rw [dif_pos hi]
        simp only [Set.mem_iInter] at hx
        apply hx
        exact (invg âŸ¨i, hiâŸ©).2.1
      Â· intro x hx
        apply Set.mem_iInterâ‚‚_of_mem
        simp only [Finset.mem_image, Set.iInter_exists, Set.biInter_and',
          Set.iInter_iInter_eq_right, Set.mem_iInter, s, fâ‚, invg] at hx
        intro i' hi'
        have hx := hx i' hi'
        rw [dif_pos (âŸ¨i', âŸ¨hi', rflâŸ©âŸ© : âˆƒ a âˆˆ s', g a = g i')] at hx
        have hâ‚€ : g i' âˆˆ Finset.image g s' := (Function.Injective.mem_finset_image hg).mpr hi'
        have : (invg âŸ¨g i', hâ‚€âŸ©).1 = i' := hg (invg âŸ¨g i', hâ‚€âŸ©).2.2
        rw [this] at hx
        exact hx
    _ = âˆ x âˆˆ s, Î¼ (fâ‚ x) := h s hâ‚
    _ = _ := by
      apply Eq.symm
      apply Finset.prod_bij (fun i' _ â†¦ g i')
      Â· intro i' hi'
        simp only [Finset.mem_image, s]
        use i'
      Â· exact fun _ _ _ _ a â†¦ hg a
      Â· intro i hi
        simp only [Finset.mem_image, s] at hi
        exact bex_def.mpr hi
      Â· intro i' hi'
        apply congrArg
        dsimp [fâ‚]
        have : g i' âˆˆ s := (Function.Injective.mem_finset_image hg).mpr hi'
        rw [dif_pos this]
        apply congrArg
        apply hg
        exact (invg âŸ¨g i', thisâŸ©).2.2.symm

variable {ğ“§ : Type*}

variable {m : â„•} {Î© : Type*} [MeasurableSpace Î©]

-- instead of using condexp, we define Y directly by integration
-- Y_k(x_0, â€¦, x_{k-1}) := âˆ« f(x_0, â€¦, x_{k-1}, X_k, â€¦, X_{m-1}) dÎ©
-- A_k(x_0, â€¦, x_{k-1}) := inf_x âˆ« f(x_0, â€¦, x_{k-1}, x, X_{k+1}, â€¦, X_{m-1}) dÎ©
-- B_k(x_0, â€¦, x_{k-1}) := sup_x âˆ« f(x_0, â€¦, x_{k-1}, x, X_{k+1}, â€¦, X_{m-1}) dÎ©
noncomputable def expressionY (Î¼ : Measure Î©) (X' : Fin m â†’ Î© â†’ ğ“§) (f' : (Fin m â†’ ğ“§) â†’ â„) (k : Fin m.succ) (Xk : Fin k â†’ ğ“§) : â„ :=
  âˆ« (x : Î©), (fun Ï‰ â†¦ f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1, hâŸ© else X' i Ï‰) x âˆ‚Î¼

noncomputable def expressionA (Î¼ : Measure Î©) (X' : Fin m â†’ Î© â†’ ğ“§) (f' : (Fin m â†’ ğ“§) â†’ â„) (k: Fin m) (Xk : Fin k â†’ ğ“§) :=
  â¨… (x : ğ“§), Î¼[fun Ï‰ â†¦ f' (fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1,hâŸ© else if i.1 = k.1 then x else X' i Ï‰)]

noncomputable def expressionB (Î¼ : Measure Î©) (X' : Fin m â†’ Î© â†’ ğ“§) (f' : (Fin m â†’ ğ“§) â†’ â„) (k: Fin m) (Xk : Fin k â†’ ğ“§) :=
  â¨† (x : ğ“§), Î¼[fun Ï‰ â†¦ f' (fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1,hâŸ© else if i.1 = k.1 then x else X' i Ï‰)]

noncomputable def expressionV (Î¼ : Measure Î©) (X' : Fin m â†’ Î© â†’ ğ“§) (f' : (Fin m â†’ ğ“§) â†’ â„) (k : Fin m) (Xk : Fin k â†’ ğ“§) :=
  fun Ï‰ â†¦ expressionY Î¼ X' f' k.succ (Fin.snoc Xk (X' k Ï‰)) - expressionY Î¼ X' f' k.castSucc Xk

variable {Î¼ : Measure Î©} [IsProbabilityMeasure Î¼]

lemma expectation_const {E : Type*} [NormedAddCommGroup E] [CompleteSpace E] [NormedSpace â„ E]
  (c : E) : âˆ« (_ : Î©), c âˆ‚Î¼ = c := by
  calc
    _ = (Î¼ Set.univ).toReal â€¢ c := by
      exact integral_const c
    _ = c := by
      have : Î¼ (Set.univ : Set Î©) = 1 := isProbabilityMeasure_iff.mp (by assumption)
      rw [this]
      simp

variable {X' : Fin m â†’ Î© â†’ ğ“§} {f' : (Fin m â†’ ğ“§) â†’ â„}

omit [IsProbabilityMeasure Î¼] in
lemma Y_snoc_eq
  (k : Fin m) (Xk : Fin k â†’ ğ“§) (x : ğ“§) :
  expressionY Î¼ X' f' k.succ (Fin.snoc Xk x) =
    Î¼[fun Ï‰ â†¦ f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1, hâŸ© else if i.1 = k.1 then x else X' i Ï‰] := by
  apply integral_congr_ae
  filter_upwards with Ï‰
  apply congr rfl
  ext i
  if h : i.1 < k.1 then
    have : i.1<(Fin.succ k).1 := by dsimp; linarith
    rw [dif_pos this, dif_pos h]
    have : âŸ¨i.1, thisâŸ© = (âŸ¨i.1, hâŸ© : Fin k).castSucc := rfl
    rw [this]
    apply Fin.snoc_castSucc
  else
    rw [dif_neg h]
    if h2 : i.1 = k.1 then
      have : i.1 < (Fin.succ k).1 := by dsimp; linarith
      rw [dif_pos this, if_pos h2]
      have : âŸ¨i.1, thisâŸ© = Fin.last k := by
        apply Eq.symm
        apply Fin.eq_mk_iff_val_eq.mpr
        exact id (Eq.symm h2)
      rw [this]
      apply Fin.snoc_last
    else
      have : Â¬ (i.1 < (Fin.succ k).1) := by
        simp only [Fin.val_succ, not_lt]
        simp only [Fin.val_fin_lt, not_lt] at h
        apply Fin.val_add_one_le_of_lt
        exact lt_of_le_of_ne h fun a => h2 (congrArg Fin.val (id (Eq.symm a)))
      rw [dif_neg this, if_neg h2]

variable {c' : Fin m â†’ â„}

lemma bound_f'
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (xâ‚€ : ğ“§):
  âˆ€ (xi : Fin m â†’ ğ“§), |f' xi| â‰¤ |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i := by
  have h (k : â„•) : (h' : k â‰¤ m) â†’ âˆ€ xi : (Fin m â†’ ğ“§), |f' xi| â‰¤
    |f' (fun i â†¦ if i.1 < k then xâ‚€ else xi i)| + âˆ‘ (i : Fin k), c' âŸ¨i.1, by linarith [i.2, h']âŸ©  := by
    induction' k with k ih
    Â· simp only [zero_le, not_lt_zero', â†“reduceIte, Finset.univ_eq_empty, Finset.sum_empty,
      add_zero, le_refl, implies_true]
    Â· intro h' xi
      apply le_trans <| ih (by linarith [h']) xi
      have : |f' (fun i â†¦ if i.1 < k then xâ‚€ else xi i)| - |f' fun i â†¦ if i.1 < k + 1 then xâ‚€ else xi i| â‰¤ c' âŸ¨k, h'âŸ© := by
        apply le_trans (abs_sub_abs_le_abs_sub _ _)
        have : (fun i â†¦ if i.1 < k + 1 then xâ‚€ else xi i) = Function.update (fun i â†¦ if i.1 < k then xâ‚€ else xi i) âŸ¨k, by linarith [h']âŸ© xâ‚€ := by
          ext i
          if hik : i.1 < k then
            have : i.1 < k+1 := by linarith
            rw [if_pos this]
            have : i â‰  âŸ¨k, by linarith [h']âŸ©:= Fin.ne_of_lt hik
            rw [Function.update_of_ne this, if_pos hik]
          else
            if hik' : i.1 = k then
              have : i.1 < k+1 := by linarith
              rw [if_pos this]
              have : i = âŸ¨k, by linarith [h']âŸ© := Fin.eq_mk_iff_val_eq.mpr hik'
              rw [this, Function.update_self]
            else
              have : Â¬ i.1 < k + 1 := by
                simp only [not_lt] at hik
                simp only [not_lt]
                apply Nat.succ_le_of_lt
                exact Nat.lt_of_le_of_ne hik (fun a â†¦ hik' (id (Eq.symm a)))
              rw [if_neg this]
              have : i â‰  âŸ¨k, by linarith [h']âŸ© := Fin.ne_of_val_ne hik'
              rw [Function.update_of_ne this, if_neg hik]
        rw [this]
        apply hfÎ¹
      have : âˆ‘ (i : Fin (k+1)), c' âŸ¨i.1, by linarith [i.2, h']âŸ© = (âˆ‘ (i : Fin k), c' âŸ¨i.1, by linarith [i.2, h']âŸ©) + c' âŸ¨k, h'âŸ© := by
        apply Fin.sum_univ_castSucc
      rw [this]
      linarith
  intro xi
  have h' := h m (Nat.le_refl m) xi
  have : (fun i : Fin m â†¦ if â†‘i < m then xâ‚€ else xi i) = fun _ â†¦ xâ‚€ := by
    ext i
    rw [if_pos i.2]
  rw [this] at h'
  exact h'

lemma hAY (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (xâ‚€ : ğ“§) (k : Fin m) (Xk : Fin k â†’ ğ“§) (x : ğ“§) :
  expressionA Î¼ X' f' k Xk â‰¤ expressionY Î¼ X' f' (Fin.succ k) (Fin.snoc Xk x) := by
  rw [Y_snoc_eq k Xk x]
  apply ciInf_le _ x
  use -(|f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i)
  intro r âŸ¨x, heqrâŸ©
  rw [â†heqr]
  apply (abs_le.mp _).1
  apply abs_expectation_le_of_abs_le_const
  filter_upwards with _
  apply bound_f' hfÎ¹ xâ‚€

lemma hBddAbove (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (xâ‚€ : ğ“§) (k : Fin m) (Xk : Fin k â†’ ğ“§) : BddAbove (Set.range fun x â†¦
  âˆ« (a : Î©), (fun Ï‰ â†¦ f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then x else X' i Ï‰) a âˆ‚Î¼):= by
  use |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i
  intro r âŸ¨x, heqrâŸ©
  rw [â†heqr]
  apply (abs_le.mp _).2
  apply abs_expectation_le_of_abs_le_const
  filter_upwards with _
  apply bound_f' hfÎ¹ xâ‚€

lemma hYB (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (xâ‚€ : ğ“§) (k : Fin m) (Xk : Fin k â†’ ğ“§) (x : ğ“§) :
  expressionB Î¼ X' f' k Xk â‰¥ expressionY Î¼ X' f' (Fin.succ k) (Fin.snoc Xk x) := by
  rw [Y_snoc_eq k Xk x]
  apply le_ciSup_of_le _ x (by apply le_refl)
  exact hBddAbove hfÎ¹ xâ‚€ k Xk

variable {mğ“§ : MeasurableSpace ğ“§}

lemma hmeasurableY
  (hX'' : âˆ€ i, Measurable (X' i))
  (hf'' : StronglyMeasurable f')
  (k : Fin m.succ) : Measurable (expressionY Î¼ X' f' k) := by
  apply stronglyMeasurable_iff_measurable.mp
  apply StronglyMeasurable.integral_prod_left
  have : (Function.uncurry fun (x : Î©) (y : Fin k.1 â†’ ğ“§) â†¦ f' fun (i : Fin m) â†¦ if h : i.1 < k.1 then y âŸ¨â†‘i, hâŸ© else X' i x)
    = f' âˆ˜ (fun xy : (Î© Ã— (Fin k.1 â†’ ğ“§)) â†¦ fun (i : Fin m) â†¦ if h : i.1 < k.1 then xy.2 âŸ¨â†‘i, hâŸ© else X' i xy.1) := rfl
  rw [this]
  apply StronglyMeasurable.comp_measurable hf''
  apply measurable_pi_lambda
  intro i
  if h : i.1 < k.1 then
    have : (fun (c : Î© Ã— (Fin k.1 â†’ ğ“§)) â†¦ if h : i.1 < k.1 then c.2 âŸ¨â†‘i, hâŸ© else X' i c.1)
      = (fun c â†¦ c âŸ¨i.1, hâŸ©) âˆ˜ Prod.snd := by
      ext c
      rw [dif_pos h]
      simp only [Nat.succ_eq_add_one, Function.comp_apply]
    rw [this]
    apply Measurable.comp
    Â· apply measurable_pi_apply
    Â· exact measurable_snd
  else
    have : (fun (c : Î© Ã— (Fin k.1 â†’ ğ“§)) â†¦ if h : i.1 < k.1 then c.2 âŸ¨â†‘i, hâŸ© else X' i c.1) = (X' i) âˆ˜ Prod.fst := by
      ext c
      rw [dif_neg h]
      simp
    rw [this]
    apply Measurable.comp
    Â· exact hX'' i
    Â· exact measurable_fst

lemma hYbdd (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (xâ‚€ : ğ“§) (k : Fin m.succ) (Xk : Fin k â†’ ğ“§) :
  |expressionY Î¼ X' f' k Xk| â‰¤ |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i := by
  apply abs_expectation_le_of_abs_le_const
  filter_upwards with _
  apply bound_f' hfÎ¹ xâ‚€

variable [hnonemptyğ“§ : Nonempty ğ“§]

lemma hintegrablelefts
  (hX'' : âˆ€ i, Measurable (X' i))
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  {t'' : â„} (ht'' : t'' â‰¥ 0) (E : â„)
  (k : â„•) (h : k â‰¤ m):
  Integrable (fun x â†¦ Real.exp (t'' * ((expressionY Î¼ X' f' âŸ¨k, Nat.lt_add_one_of_le hâŸ© fun i â†¦ X' (Fin.castLE h i) x) - E))) Î¼ := by
  constructor
  Â· apply Measurable.aestronglyMeasurable
    apply Measurable.exp
    apply Measurable.const_mul
    apply Measurable.sub_const _ E
    have : (fun x â†¦ expressionY Î¼ X' f' âŸ¨k, Nat.lt_add_one_of_le hâŸ© fun i â†¦ X' (Fin.castLE h i) x)
      = expressionY Î¼ X' f' âŸ¨k, Nat.lt_add_one_of_le hâŸ© âˆ˜ fun x â†¦ fun i â†¦ X' (Fin.castLE h i) x := rfl
    rw [this]
    apply (hmeasurableY hX'' hf'' âŸ¨k, Nat.lt_add_one_of_le hâŸ©).comp
    apply measurable_pi_lambda
    intro _
    apply hX''
  Â· let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
    let bdf := |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i
    apply @MeasureTheory.HasFiniteIntegral.of_bounded _ _ _ _ _ _ _ (t'' * (bdf-E)).exp
    filter_upwards with Ï‰
    dsimp only [norm]
    rw [Real.abs_exp]
    apply Real.exp_le_exp.mpr
    apply mul_le_mul_of_nonneg_left _ ht''
    apply tsub_le_tsub_right _ E
    apply le_of_max_le_left
    apply hYbdd hfÎ¹ xâ‚€ âŸ¨k, Nat.lt_add_one_of_le hâŸ© fun i â†¦ X' (Fin.castLE h i) Ï‰

lemma hintegrableAB
  (hX'' : âˆ€ i, Measurable (X' i))
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  (k : Fin m) (Xk : Fin k â†’ ğ“§) (x : ğ“§) :
  Integrable (fun a â†¦ f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then x else X' i a) Î¼ := by
  let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
  constructor
  Â· apply @AEStronglyMeasurable.comp_aemeasurable (Fin m â†’ ğ“§) â„ _ f' Î©
    apply hf''.aestronglyMeasurable
    apply Measurable.aemeasurable
    apply measurable_pi_iff.mpr
    intro i
    if hâ‚€ : i.1 < k.1 then
      have : (fun x_1 â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then x else X' i x_1) = fun _ â†¦ Xk âŸ¨i.1, hâ‚€âŸ© := by
        ext x
        rw [dif_pos hâ‚€]
      rw [this]
      exact measurable_const
    else
      if hâ‚ : i.1 = k.1 then
        have : (fun x_1 â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then x else X' i x_1) = fun _ â†¦ x := by
          ext x
          rw [dif_neg hâ‚€, if_pos hâ‚]
        rw [this]
        exact measurable_const
      else
        have : (fun x_1 â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then x else X' i x_1) = fun x_1 â†¦ X' i x_1 := by
          ext x
          rw [dif_neg hâ‚€, if_neg hâ‚]
        rw [this]
        exact hX'' i
  Â· apply MeasureTheory.HasFiniteIntegral.of_bounded _
    exact |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i
    filter_upwards with _
    apply bound_f' hfÎ¹ xâ‚€

lemma hAB
  (hX'' : âˆ€ i, Measurable (X' i))
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  (k : Fin m) (Xk : Fin k â†’ ğ“§) :
  expressionB Î¼ X' f' k Xk â‰¤ expressionA Î¼ X' f' k Xk + c' k := by
  let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
  apply le_ciInf_add
  intro xâ‚
  apply (ciSup_le_iff (hBddAbove hfÎ¹ xâ‚€ k Xk)).mpr
  intro xâ‚‚
  calc
    _ â‰¤ âˆ« (Ï‰ : Î©), (f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then xâ‚ else X' i Ï‰) + c' k âˆ‚Î¼ := by
      apply integral_mono
      exact hintegrableAB hX'' hfÎ¹ hf'' k Xk xâ‚‚
      exact integrable_add_const_iff.mpr (hintegrableAB hX'' hfÎ¹ hf''  k Xk xâ‚)
      intro Ï‰
      have : (fun i : Fin m â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then xâ‚ else X' i Ï‰)
        = Function.update (fun i : Fin m â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then xâ‚‚ else X' i Ï‰) k xâ‚ := by
          ext i
          if h : i.1 < k.1 then
            have : i â‰  k := Fin.ne_of_lt h
            rw [dif_pos h, Function.update_of_ne this, dif_pos h]
          else
            rw [dif_neg h]
            if h': i.1 = k.1 then
              have : i=k :=  Fin.eq_of_val_eq h'
              rw [if_pos h', this, Function.update_self]
            else
              rw [if_neg h']
              have : i â‰  k := fun a â†¦ h' (congrArg Fin.val a)
              rw [Function.update_of_ne this, dif_neg h, if_neg h']
      dsimp
      rw [this]
      apply tsub_le_iff_left.mp
      apply le_of_max_le_left
      apply hfÎ¹
    _ = (âˆ« (Ï‰ : Î©), f' fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if i.1 = k.1 then xâ‚ else X' i Ï‰ âˆ‚Î¼)
        + (âˆ« (_ : Î©), c' k âˆ‚Î¼) := integral_add (hintegrableAB hX'' hfÎ¹ hf'' k Xk xâ‚) (integrable_const (c' k))
    _ = _ := add_left_cancel_iff.mpr (expectation_const (c' k))

-- we use independency for the martingale property
-- âˆ« Y(x_0, â€¦, x_{k-1}, X_k) dÎ© = Y(x_0, â€¦, x_{m-1})
-- use double_integral_indep_eq_integral
lemma hmartingale
  (hX'' : âˆ€ i, Measurable (X' i))
  (hIndep' : iIndepFun X' Î¼)
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  (k : Fin m) (Xk : Fin k â†’ ğ“§) :
  âˆ« (Ï‰ : Î©), expressionY Î¼ X' f' k.succ (Fin.snoc Xk (X' k Ï‰)) âˆ‚Î¼ = expressionY Î¼ X' f' k.castSucc Xk := by
  let S : Finset (Fin m) := {i : Fin m | i.1 > k.1}
  let T : Finset (Fin m) := {k}
  have hST : Disjoint T S := by
    apply Finset.disjoint_singleton_left.mpr
    simp only [gt_iff_lt, Fin.val_fin_lt, Finset.mem_filter, Finset.mem_univ, lt_self_iff_false,
      and_false, not_false_eq_true, S]
  have hindep := ProbabilityTheory.iIndepFun.indepFun_finset T S hST hIndep' hX''
  let toelS (i : Fin m) (h : Â¬ i.1 < k.1) (h' : Â¬ i.1 = k.1): S := by
    use i
    simp only [gt_iff_lt, Fin.val_fin_lt, Finset.mem_filter, Finset.mem_univ, true_and, S]
    simp only [Fin.val_fin_lt, not_lt] at h
    exact lt_of_le_of_ne h fun a â†¦ h' (congrArg Fin.val (id (Eq.symm a)))
  let elT : T := âŸ¨k, Finset.mem_singleton.mpr rflâŸ©
  let F : (T â†’ ğ“§) Ã— (S â†’ ğ“§) â†’ â„ := fun âŸ¨t,sâŸ© â†¦
    f' (fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1,hâŸ©
      else if h': i.1 = k.1 then t elT
      else s (toelS i h h'))
  let gT : Î© â†’ T â†’ ğ“§ := fun Ï‰ t â†¦ X' t.1 Ï‰
  let gS : Î© â†’ S â†’ ğ“§ := fun Ï‰ s â†¦ X' s.1 Ï‰

  have hlefteq  : âˆ« (Ï‰ : Î©), expressionY Î¼ X' f' k.succ (Fin.snoc Xk (X' k Ï‰)) âˆ‚Î¼
    = âˆ« (Ï‰ : Î©), âˆ« (a : Î©), F âŸ¨(gT Ï‰), (gS a)âŸ© âˆ‚Î¼ âˆ‚Î¼ := by
    dsimp only [F]
    apply integral_congr_ae
    filter_upwards with Ï‰
    apply integral_congr_ae
    filter_upwards with a
    apply congr rfl
    ext i
    if h : i.1 < k.1 then
      rw [dif_pos h]
      have : i.1 < k.succ := Nat.lt_succ_of_lt h
      rw [dif_pos this]
      dsimp
      have : (âŸ¨i.1, thisâŸ© : Fin k.succ) = (âŸ¨i.1,hâŸ© : Fin k).castSucc := rfl
      rw [this, Fin.snoc_castSucc]
    else
      rw [dif_neg h]
      if h' : i.1 = k.1 then
        rw [dif_pos h', h']
        have : k.1 < k.succ := Nat.lt_add_one k.1
        rw [dif_pos this]
        have : âŸ¨k.1, thisâŸ© = Fin.last k := rfl
        rw [this, Fin.snoc_last]
      else
        rw [dif_neg h']
        have : Â¬ i.1 < k.succ := by
          simp only [Fin.val_succ, not_lt]
          simp only [Fin.val_fin_lt, not_lt] at h
          exact Nat.lt_of_le_of_ne h fun a â†¦ h' (id (Eq.symm a))
        rw [dif_neg this]
  apply Eq.trans hlefteq
  have hrighteq : expressionY Î¼ X' f' k.castSucc Xk = âˆ« (Ï‰ : Î©), F âŸ¨(gT Ï‰), (gS Ï‰)âŸ© âˆ‚Î¼ := by
    dsimp only [F]
    apply integral_congr_ae
    filter_upwards with Ï‰
    apply congr rfl
    ext i
    if h : i.1 < k.1 then
      rw [dif_pos h]
      have : i.1 < k.castSucc.1 := h
      rw [dif_pos this]
    else
      rw [dif_neg h]
      have : Â¬ i.1 < k.castSucc.1 := h
      rw [dif_neg this]
      if h' : i.1 = k.1 then
        rw [dif_pos h']
        dsimp [gT]
        have : i = k := Fin.eq_of_val_eq h'
        rw [this]
      else
        rw [dif_neg h']
  apply Eq.trans _ hrighteq.symm
  apply double_integral_indep_eq_integral
  Â· apply StronglyMeasurable.comp_measurable hf''
    apply measurable_pi_iff.mpr
    intro i
    if h : i < k then
      have : (fun x : (T â†’ ğ“§) Ã— (S â†’ ğ“§) â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if h' : i.1 = k.1 then x.1 elT else x.2 (toelS i h h')) = fun _ â†¦ Xk âŸ¨i.1, hâŸ© := by
        simp only [Fin.val_fin_lt]
        ext x
        rw [dif_pos h]
      rw [this]
      exact measurable_const
    else
      if h' : i.1 = k.1 then
        have : (fun x : (T â†’ ğ“§) Ã— (S â†’ ğ“§) â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if h' : i.1 = k.1 then x.1 elT else x.2 (toelS i h h')) = fun x â†¦ x.1 elT := by
          simp only [Fin.val_fin_lt]
          ext x
          rw [dif_neg h, dif_pos h']
        rw [this]
        exact Measurable.eval measurable_fst
      else
        have : (fun x : (T â†’ ğ“§) Ã— (S â†’ ğ“§) â†¦ if h : i.1 < k.1 then Xk âŸ¨â†‘i, hâŸ© else if h' : i.1 = k.1 then x.1 elT else x.2 (toelS i h h')) = fun x â†¦ x.2 (toelS i h h') := by
          simp only [Fin.val_fin_lt]
          ext x
          rw [dif_neg h, dif_neg h']
        rw [this]
        exact Measurable.eval measurable_snd
  Â· apply Measurable.aemeasurable
    exact measurable_pi_lambda gT fun a â†¦ hX'' â†‘a
  Â· apply Measurable.aemeasurable
    exact measurable_pi_lambda gS fun a â†¦ hX'' â†‘a
  Â· exact hindep
  Â· let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
    apply @MeasureTheory.HasFiniteIntegral.of_bounded _ _ _ _ _ _ F (|f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i)
    filter_upwards with âŸ¨t, sâŸ©
    apply bound_f' hfÎ¹ xâ‚€

lemma hhoeffding_V
  (hX'' : âˆ€ i, Measurable (X' i))
  (hIndep' : iIndepFun X' Î¼)
  {c' : Fin m â†’ â„}
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f') (t'' : â„)
  (k : Fin m) (Xk : Fin k â†’ ğ“§) : âˆ« (Ï‰ : Î©), (t'' * (expressionV Î¼ X' f' k Xk Ï‰)).exp âˆ‚Î¼ â‰¤ (t''^2 * (c' k)^2 / 8).exp := by
  let Y : Î  (k: Fin m.succ), (Fin k â†’ ğ“§) â†’ â„ := expressionY Î¼ X' f'
  let A : Î  (k: Fin m), (Fin k â†’ ğ“§) â†’ â„ := expressionA Î¼ X' f'
  let B : Î  (k: Fin m), (Fin k â†’ ğ“§) â†’ â„ := expressionB Î¼ X' f'
  let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
  let a := A k Xk - Y k.castSucc Xk
  let b := B k Xk - Y k.castSucc Xk
  have hmeasurable : Measurable (fun x â†¦ Fin.snoc Xk (X' k x) : Î© â†’ Fin (k.1+1) â†’ ğ“§):= by
    apply measurable_pi_lambda
    intro i
    if h : i.1 < k.1 then
        have : (fun Ï‰ â†¦ (Fin.snoc Xk (X' k Ï‰) : Fin k.succ â†’ ğ“§) i) = fun _ â†¦ Xk âŸ¨i.1, hâŸ© := by
          ext Ï‰
          dsimp [Fin.snoc]
          rw [dif_pos h]
          exact rfl
        rw [this]
        exact measurable_const
    else
      have : (fun Ï‰ â†¦ (Fin.snoc Xk (X' k Ï‰) : Fin k.succ â†’ ğ“§) i) = fun Ï‰ â†¦ X' k Ï‰ := by
        ext Ï‰
        dsimp [Fin.snoc]
        rw [dif_neg h]
      rw [this]
      exact hX'' k
  calc
    _ â‰¤ ((t''^2 * (b - a)^2 / 8).exp : â„) := by
      apply hoeffding Î¼ t'' a b
      Â· apply Measurable.aemeasurable
        apply Measurable.add_const _ (-Y k.castSucc Xk)
        apply (hmeasurableY hX'' hf'' k.succ).comp
        exact hmeasurable
      Â· filter_upwards with Ï‰
        dsimp only [Set.Icc]
        constructor
        Â· exact tsub_le_tsub_right (hAY hfÎ¹ xâ‚€ k Xk (X' k Ï‰)) (Y k.castSucc Xk)
        Â· exact tsub_le_tsub_right (hYB hfÎ¹ xâ‚€ k Xk (X' k Ï‰)) (Y k.castSucc Xk)
      Â· calc
          _ = (âˆ« (Ï‰ : Î©), Y k.succ (Fin.snoc Xk (X' k Ï‰)) âˆ‚Î¼) - âˆ« (_ : Î©), Y k.castSucc Xk âˆ‚Î¼ := by
            apply integral_sub
            Â· constructor
              Â· apply aestronglyMeasurable_iff_aemeasurable.mpr
                apply Measurable.comp_aemeasurable'
                Â· exact hmeasurableY hX'' hf'' k.succ
                Â· apply Measurable.aemeasurable
                  exact hmeasurable
              Â· apply MeasureTheory.HasFiniteIntegral.of_bounded _
                exact max (B k Xk) (-(A k Xk))
                filter_upwards with Ï‰
                calc
                  _ â‰¤ max (Y k.succ (Fin.snoc Xk (X' k Ï‰))) (-Y k.succ (Fin.snoc Xk (X' k Ï‰))) :=
                    Preorder.le_refl â€–Y k.succ (Fin.snoc Xk (X' k Ï‰))â€–
                  _ â‰¤ max (Y k.succ (Fin.snoc Xk (X' k Ï‰))) (-(A k Xk)) := by
                    apply max_le_max_left (Y k.succ (Fin.snoc Xk (X' k Ï‰)))
                    exact neg_le_neg_iff.mpr (hAY hfÎ¹ xâ‚€ k Xk (X' k Ï‰))
                  _ â‰¤ _ := max_le_max_right (-A k Xk) (hYB hfÎ¹ xâ‚€ k Xk (X' k Ï‰))
            Â· exact integrable_const (Y k.castSucc Xk)
          _ = (âˆ« (Ï‰ : Î©), Y k.succ (Fin.snoc Xk (X' k Ï‰)) âˆ‚Î¼) - Y k.castSucc Xk := by
            apply sub_right_inj.mpr
            exact expectation_const (Y k.castSucc Xk)
          _ = _ := sub_eq_zero_of_eq (hmartingale hX'' hIndep' hfÎ¹ hf'' k Xk)
    _ â‰¤ _ := by
      apply Real.exp_le_exp.mpr
      apply div_le_div_of_nonneg_right _ (by norm_num)
      apply mul_le_mul_of_nonneg_left _ (sq_nonneg t'')
      have : b - a = B k Xk - A k Xk := by simp only [sub_sub_sub_cancel_right, b, a]
      rw [this]
      apply pow_le_pow_leftâ‚€ _ _ 2
      Â· apply sub_nonneg.mpr
        let c : â„ := Î¼[fun Ï‰ â†¦ f' (fun i â†¦ if h : i.1 < k.1 then Xk âŸ¨i.1,hâŸ© else if i.1 = k.1 then xâ‚€ else X' i Ï‰)]
        calc
          _ â‰¤ c := le_of_le_of_eq (hAY hfÎ¹ xâ‚€ k Xk xâ‚€) (Y_snoc_eq k Xk xâ‚€)
          _ â‰¤ _ := (le_iff_le_of_cmp_eq_cmp (congrFun (congrArg cmp (Y_snoc_eq k Xk xâ‚€)) (B k Xk))).mp
                (hYB hfÎ¹ xâ‚€ k Xk xâ‚€)
      Â· exact tsub_le_iff_left.mpr (hAB hX'' hfÎ¹ hf'' k Xk)

lemma heqind
  (hX'' : âˆ€ i, Measurable (X' i))
  (hIndep' : iIndepFun X' Î¼)
  {c' : Fin m â†’ â„}
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  {t'' : â„} (ht'' : t'' â‰¥ 0)
  (xâ‚€ : ğ“§) (k : â„•) (h : k â‰¤ m) :
  âˆ« (Ï‰ : Î©), (t'' *(expressionY Î¼ X' f' âŸ¨k,Nat.lt_add_one_of_le hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h i) Ï‰) - expressionY Î¼ X' f' 0 (fun _ â†¦ xâ‚€))).exp âˆ‚Î¼
    â‰¤ ((t'' ^2 * âˆ‘ (i : Fin k), c' (Fin.castLE h i) ^ 2)/8).exp := by
  let Y : Î  (k: Fin m.succ), (Fin k â†’ ğ“§) â†’ â„ := expressionY Î¼ X' f'
  let V : Î  (k : Fin m), (Fin k â†’ ğ“§) â†’ Î© â†’ â„ := expressionV Î¼ X' f'
  let bdf := |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i
  let E := Y 0 (fun _ â†¦ xâ‚€)

  induction' k with k ih
  Â· simp only [Nat.succ_eq_add_one, Fin.val_zero, not_lt_zero', â†“reduceDIte,
    sub_self, mul_zero, Real.exp_zero, integral_const, probReal_univ, smul_eq_mul, mul_one,
    Finset.univ_eq_empty, Finset.sum_empty, zero_div, le_refl, expressionY]
  Â· have ih := ih <| Nat.le_of_succ_le h
    calc
      _ = âˆ« (Ï‰ : Î©), âˆ« (Ï‰' : Î©), (t'' *(Y âŸ¨k+1,Nat.lt_add_one_of_le hâŸ©
          (Fin.snoc (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) (X' âŸ¨k,hâŸ© Ï‰')) - E)).exp âˆ‚Î¼ âˆ‚Î¼ := by
        let S : Finset (Fin m) := {i : Fin m | i.1 < k}
        let T : Finset (Fin m) := {âŸ¨k,hâŸ©}
        have hST : Disjoint S T := by
          apply Finset.disjoint_singleton_right.mpr
          simp only [Finset.mem_filter, Finset.mem_univ, lt_self_iff_false, and_false,
            not_false_eq_true, S]
        have hindep := ProbabilityTheory.iIndepFun.indepFun_finset S T hST hIndep' hX''
        let toelS (i : Fin k) : S := by
          use (Fin.castLE h (Fin.castSucc i))
          simp only [Fin.castLE_castSucc, Finset.mem_filter, Finset.mem_univ,
            Fin.val_castLE, Fin.is_lt, and_self, S]
        let elT : T := âŸ¨âŸ¨k,hâŸ©, Finset.mem_singleton.mpr rflâŸ©
        let F : (S â†’ ğ“§) Ã— (T â†’ ğ“§) â†’ â„ := fun âŸ¨s,tâŸ© â†¦
          Real.exp (t'' * (Y âŸ¨k + 1, Nat.lt_add_one_of_le hâŸ© (Fin.snoc (fun i â†¦ s (toelS i)) (t elT)) - E))
        let gT : Î© â†’ T â†’ ğ“§ := fun Ï‰ t â†¦ X' t.1 Ï‰
        let gS : Î© â†’ S â†’ ğ“§ := fun Ï‰ s â†¦ X' s.1 Ï‰
        have hlefteq : âˆ« (Ï‰ : Î©), Real.exp (t'' * ((Y âŸ¨k + 1, Nat.lt_add_one_of_le hâŸ© fun i â†¦ X' (Fin.castLE h i) Ï‰) - E)) âˆ‚Î¼
          = âˆ« (Ï‰ : Î©), F âŸ¨gS Ï‰, gT Ï‰âŸ© âˆ‚Î¼ := by
          apply congrArg
          ext Ï‰
          apply congrArg
          apply congrArg
          apply sub_left_inj.mpr
          apply congrArg
          ext i
          if h': i.1 < k then
            dsimp only [Fin.snoc]
            rw [dif_pos h']
            congr
          else
            dsimp only [Fin.snoc]
            rw [dif_neg h']
            simp only [cast_eq, gT]
            have : i.1 = k := by
              simp only [Nat.succ_eq_add_one, not_lt] at h'
              exact Nat.eq_of_le_of_lt_succ h' i.2
            apply congrFun
            apply congrArg
            exact Fin.eq_mk_iff_val_eq.mpr this
        have hrighteq : âˆ« (Ï‰ : Î©), âˆ« (Ï‰' : Î©), Real.exp (t'' * (Y âŸ¨k + 1, Nat.lt_add_one_of_le hâŸ©
          (Fin.snoc (fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) (X' âŸ¨k, hâŸ© Ï‰')) - E)) âˆ‚Î¼ âˆ‚Î¼
          = âˆ« (Ï‰ : Î©), âˆ« (Ï‰' : Î©), F âŸ¨gS Ï‰, gT Ï‰'âŸ© âˆ‚Î¼ âˆ‚Î¼ := by
          apply congrArg
          ext Ï‰
          apply congrArg
          ext Ï‰'
          apply congrArg
          apply congrArg
          apply sub_left_inj.mpr
          apply congrArg
          ext i
          apply congrArgâ‚‚
          Â· dsimp [gT]
          Â· dsimp [gS]
        rw [hlefteq, hrighteq]
        apply Eq.symm

        apply double_integral_indep_eq_integral
        Â· apply StronglyMeasurable.comp_measurable Real.measurable_exp.stronglyMeasurable
          apply Measurable.comp (measurable_const_mul t'')
          apply Measurable.sub_const _ E
          exact MeasurableSpace.pi
          exact MeasurableSpace.pi
          apply Measurable.comp (hmeasurableY hX'' hf'' âŸ¨k + 1, Nat.lt_add_one_of_le hâŸ©)
          apply measurable_pi_iff.mpr
          intro i
          if h' : i.1 < k then
            have : (fun x : (S â†’ ğ“§) Ã— (T â†’ ğ“§) â†¦ @Fin.snoc k (fun _ â†¦ ğ“§) (fun i â†¦ x.1 (toelS i)) (x.2 elT) i)
              = (fun x â†¦ x (toelS âŸ¨i, h'âŸ©)) âˆ˜ Prod.fst := by
              ext x
              dsimp [Fin.snoc]
              rw [dif_pos h']
              rfl
            rw [this]
            apply Measurable.comp _ measurable_fst
            exact measurable_pi_apply (toelS âŸ¨â†‘i, h'âŸ©)
          else
            have : (fun x : (S â†’ ğ“§) Ã— (T â†’ ğ“§) â†¦ @Fin.snoc k (fun _ â†¦ ğ“§) (fun i â†¦ x.1 (toelS i)) (x.2 elT) i)
              = (fun x â†¦ x elT) âˆ˜ Prod.snd := by
              ext x
              dsimp [Fin.snoc]
              rw [dif_neg h']
            rw [this]
            apply Measurable.comp _ measurable_snd
            exact measurable_pi_apply elT
        Â· apply Measurable.aemeasurable
          exact measurable_pi_lambda gS fun a â†¦ hX'' â†‘a
        Â· apply Measurable.aemeasurable
          exact measurable_pi_lambda gT fun a â†¦ hX'' â†‘a
        Â· exact hindep
        Â· apply @MeasureTheory.HasFiniteIntegral.of_bounded _ _ _ _ _ _ F (t''*(bdf-E)).exp
          filter_upwards with âŸ¨a, tâŸ©
          dsimp only [F, norm]
          rw [Real.abs_exp]
          apply Real.exp_le_exp.mpr
          apply mul_le_mul_of_nonneg_left _ ht''
          apply tsub_le_tsub_right _ E
          apply le_of_max_le_left
          apply hYbdd hfÎ¹ xâ‚€
      _ = âˆ« (Ï‰ : Î©), âˆ« (Ï‰' : Î©), (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp
        * (t'' *(V âŸ¨k,hâŸ© fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) Ï‰').exp âˆ‚Î¼ âˆ‚Î¼ := by
        dsimp only [V]
        apply congrArg
        ext Ï‰
        apply congrArg
        ext Ï‰'
        calc
          _ = (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)
          + t'' *(V âŸ¨k,hâŸ© fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) Ï‰').exp := by
            apply congrArg
            rw [â†mul_add]
            apply congrArg
            dsimp only [V, Y, expressionV]
            simp only [Nat.succ_eq_add_one, Fin.castLE_castSucc, Fin.succ_mk,
              Fin.castSucc_mk, sub_add_sub_cancel']
          _ = _ := by apply Real.exp_add
      _ = âˆ« (Ï‰ : Î©), (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp
        * âˆ« (Ï‰' : Î©), (t'' *(V âŸ¨k,hâŸ© fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) Ï‰').exp âˆ‚Î¼ âˆ‚Î¼ := by
        apply congrArg
        ext Ï‰
        exact
          integral_const_mul
            (Real.exp (t'' * ((Y âŸ¨k, Nat.lt_succ_of_lt hâŸ© fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)))
            fun a â†¦ Real.exp (t'' * V âŸ¨k, hâŸ© (fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) a)
      _ â‰¤ âˆ« (Ï‰ : Î©), (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp
        * (t'' ^2 * (c' âŸ¨k, hâŸ©)^2 / 8).exp âˆ‚Î¼ := by
        have (h : k â‰¤ m) :
          Integrable (fun x â†¦ Real.exp (t'' * ((Y âŸ¨k, Nat.lt_add_one_of_le hâŸ© fun i â†¦ X' (Fin.castLE h i) x) - E))) Î¼ :=
            hintegrablelefts hX'' hfÎ¹ hf'' ht'' E k h
        have hintegrableleft := this (Nat.le_of_succ_le h)
        simp only [Nat.succ_eq_add_one] at hintegrableleft
        apply integral_mono
        Â· have : (fun (Ï‰ : Î©) â†¦ (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp
          * âˆ« (Ï‰' : Î©), (t'' *(V âŸ¨k,hâŸ© fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) Ï‰').exp âˆ‚Î¼)
          = (fun (Ï‰ : Î©) â†¦ (âˆ« (Ï‰' : Î©), (t'' *(V âŸ¨k,hâŸ© fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) Ï‰').exp âˆ‚Î¼)
        * (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp) := by
            ext Ï‰
            rw [mul_comm]
          rw [this]
          apply Integrable.bdd_mul (c:= (t'' * (bdf + bdf)).exp)
          Â· simp only [Nat.succ_eq_add_one, Fin.castLE_castSucc]
            exact hintegrableleft
          Â· apply StronglyMeasurable.aestronglyMeasurable
            apply StronglyMeasurable.integral_prod_left
            apply StronglyMeasurable.comp_measurable Real.measurable_exp.stronglyMeasurable
            apply Measurable.comp
            Â· exact measurable_const_mul t''
            Â· apply Measurable.sub
              Â· apply (hmeasurableY hX'' hf'' (âŸ¨k, hâŸ© : Fin m).succ).comp
                apply measurable_pi_lambda
                intro i
                if h' : i.1 < k then
                  have : (fun c : Î© Ã— Î© â†¦
                    @Fin.snoc k (fun _ â†¦ ğ“§) (fun i : Fin k â†¦ X' (Fin.castLE h (Fin.castSucc i)) c.2) (X' âŸ¨k, hâŸ© c.1) i)
                    = fun c â†¦ X' (Fin.castLE h i) c.2 := by
                    ext c
                    dsimp only [Fin.snoc]
                    rw [dif_pos h']
                    simp
                  rw [this]
                  apply (hX'' _).comp measurable_snd
                else
                  have : (fun c : Î© Ã— Î© â†¦
                    @Fin.snoc k (fun _ â†¦ ğ“§) (fun i : Fin k â†¦ X' (Fin.castLE h (Fin.castSucc i)) c.2) (X' âŸ¨k, hâŸ© c.1) i)
                    = fun c â†¦ X' âŸ¨k, hâŸ© c.1 := by
                    ext c
                    dsimp only [Fin.snoc]
                    rw [dif_neg h']
                    simp
                  rw [this]
                  apply (hX'' _).comp measurable_fst
              Â· have : (fun a : Î© Ã— Î© â†¦ Y (âŸ¨k, hâŸ© : Fin m).castSucc fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) a.2)
                  = (Y (âŸ¨k, hâŸ© : Fin m).castSucc) âˆ˜ (fun a : Î© â†¦ fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) a) âˆ˜ Prod.snd := rfl
                dsimp
                dsimp at this
                rw [this]
                apply (hmeasurableY hX'' hf'' (âŸ¨k, hâŸ© : Fin m).castSucc).comp
                apply Measurable.comp _ measurable_snd
                apply measurable_pi_lambda
                intro i
                apply hX''
          Â· filter_upwards with Ï‰
            apply abs_expectation_le_of_abs_le_const
            filter_upwards with Ï‰'
            rw [Real.abs_exp]
            apply Real.exp_le_exp.mpr
            apply mul_le_mul_of_nonneg_left _ ht''
            dsimp only [V]
            apply (le_abs.mpr (Or.inl le_rfl)).trans
            apply (abs_sub _ _).trans
            apply add_le_add
            Â· apply hYbdd hfÎ¹ xâ‚€
            Â· apply hYbdd hfÎ¹ xâ‚€ (âŸ¨k, hâŸ© : Fin m).castSucc fun i â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰
        Â· apply Integrable.mul_const (by simp; exact hintegrableleft) (Real.exp (t'' ^ 2 * c' âŸ¨k, hâŸ© ^ 2 / 8))
        Â· intro Ï‰
          apply mul_le_mul_of_nonneg_left
          Â· apply hhoeffding_V hX'' hIndep' hfÎ¹ hf'' t''
          Â· apply Real.exp_nonneg
      _ = (âˆ« (Ï‰ : Î©), (t'' *(Y âŸ¨k,Nat.lt_succ_of_lt hâŸ© (fun (i : Fin k) â†¦ X' (Fin.castLE h (Fin.castSucc i)) Ï‰) - E)).exp âˆ‚Î¼)
        * (t'' ^2 * (c' âŸ¨k, hâŸ©)^2 / 8).exp := by
        apply integral_mul_const
      _ â‰¤ Real.exp ((t'' ^2 * âˆ‘ i : Fin k, c' (Fin.castLE (Nat.le_of_succ_le h) i) ^ 2) / 8)
        * (t'' ^2 * (c' âŸ¨k, hâŸ©)^2 / 8).exp := by
        apply mul_le_mul_of_nonneg_right
        Â· simp only [Nat.succ_eq_add_one, Fin.castLE_castSucc]
          exact ih
        Â· apply Real.exp_nonneg
      _ = Real.exp ((t'' ^2 * âˆ‘ i : Fin k, c' (Fin.castLE (Nat.le_of_succ_le h) i) ^ 2) / 8
        + t'' ^2 * (c' âŸ¨k, hâŸ©)^2 / 8) := by
          apply Eq.symm
          apply Real.exp_add
      _ = _ := by
        apply congrArg
        rw [â†add_div, â†mul_add]
        apply congrArg (fun (a:â„) â†¦ t''^2 * a / 8)
        exact Eq.symm (Fin.sum_univ_castSucc fun i â†¦ c' (Fin.castLE h i) ^ 2)

theorem mcdiarmid_inequality_aux
  (hX'' : âˆ€ i, Measurable (X' i))
  (hIndep' : iIndepFun X' Î¼)
  {c' : Fin m â†’ â„}
  (hfÎ¹ : âˆ€ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : StronglyMeasurable f')
  {Îµ : â„} (hÎµ : Îµ â‰¥ 0)
  {t : â„} (ht : t â‰¥ 0) (ht' : t * âˆ‘ i, (c' i) ^ 2 â‰¤ 1) :
  (Î¼ (fun Ï‰ : Î© â†¦ (f' âˆ˜ (Function.swap X')) Ï‰ - Î¼[f' âˆ˜ (Function.swap X')] â‰¥ Îµ)).toReal â‰¤
    (-2 * Îµ ^ 2 * t).exp := by

  let Y : Î  (k: Fin m.succ), (Fin k â†’ ğ“§) â†’ â„ := expressionY Î¼ X' f'

  -- inequalities about Y, A, B are not hard to prove
  let xâ‚€ : ğ“§ := (Classical.inhabited_of_nonempty hnonemptyğ“§).default
  let bdf := |f' (fun _ â†¦ xâ‚€)| + âˆ‘ i : Fin m, c' i

  let t'' := 4 * Îµ * t
  have ht'' : t'' â‰¥ 0 := by
    apply mul_nonneg
    apply mul_nonneg
    norm_num
    apply hÎµ
    exact ht

  let E := Y 0 (fun _ â†¦ xâ‚€)

  have := heqind hX'' hIndep' hfÎ¹ hf'' ht'' xâ‚€  m le_rfl
  simp only [Nat.succ_eq_add_one, Fin.castLE_rfl, id_eq, Fin.is_lt, â†“reduceDIte, Fin.eta,
    integral_const, smul_eq_mul, Fin.val_zero,
    not_lt_zero', expressionY] at this
  have hintegrable :
    Integrable (fun x â†¦ Real.exp (t'' * ((Y âŸ¨m, Nat.lt_add_one_of_le le_rflâŸ© fun i â†¦ X' (Fin.castLE le_rfl i) x) - E))) Î¼
    := hintegrablelefts hX'' hfÎ¹ hf'' ht'' E m le_rfl
  simp only [Nat.succ_eq_add_one, Fin.castLE_rfl, id_eq, Fin.is_lt, â†“reduceDIte, Fin.eta,
    integral_const, smul_eq_mul, Fin.val_zero,
    not_lt_zero', Y, expressionY, E] at hintegrable
  convert (ProbabilityTheory.measure_ge_le_exp_mul_mgf Îµ ht'' hintegrable).trans _
  Â· simp only [Function.comp_apply, ge_iff_le, probReal_univ, one_mul]
    rfl
  dsimp only [mgf]
  calc
    _ â‰¤ Real.exp (-t'' * Îµ) * Real.exp ((t'' ^ 2 * âˆ‘ x : Fin m, c' x ^ 2) / 8) := by
      apply mul_le_mul_of_nonneg_left this
      apply Real.exp_nonneg
    _ â‰¤ _ := by
      rw [â†Real.exp_add]
      apply Real.exp_monotone
      simp only [neg_mul, neg_add_le_iff_le_add, le_add_neg_iff_add_le, t'']
      calc
        _ = 2 * Îµ ^ 2 * t * (t * âˆ‘ x : Fin m, c' x ^ 2) + 2 * Îµ ^ 2 * t := by ring
        _ â‰¤ 2 * Îµ ^ 2 * t * 1 + 2 * Îµ ^ 2 * t := by
          apply add_le_add_left
          apply mul_le_mul_of_nonneg_left ht'
          apply mul_nonneg _ ht
          norm_num
          exact sq_nonneg Îµ
        _ = _ := by ring

variable {Î¹ : Type*} [DecidableEq Î¹]

omit hnonemptyğ“§ in
lemma bounded_difference_iff (f : (Î¹ â†’ ğ“§) â†’ â„) (c : Î¹ â†’ â„):
  (âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), |f x - f (Function.update x i x')| â‰¤ c i)
   â†” âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), f x - f (Function.update x i x') â‰¤ c i := by
  constructor
  Â· intro h i xi x'
    exact le_of_max_le_left (h i xi x')
  Â· intro h i xi x'
    apply abs_sub_le_iff.mpr
    constructor
    Â· exact h i xi x'
    Â· have : xi = Function.update (Function.update xi i x') i (xi i) := by
        simp only [Function.update_idem, Function.update_eq_self]
      nth_rewrite 2 [this]
      exact h i (Function.update xi i x') (xi i)

variable  [Fintype Î¹]

theorem mcdiarmid_inequality_pos
  (X : Î¹ â†’ Î© â†’ ğ“§) (hX : âˆ€ i, Measurable (X i))
  (hX' : iIndepFun X Î¼) {f : (Î¹ â†’ ğ“§) â†’ â„}
  {c : Î¹ â†’ â„}
  (hf : âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), |f x - f (Function.update x i x')| â‰¤ c i)
  (hf' : Measurable f)
  {Îµ : â„} (hÎµ : Îµ â‰¥ 0)
  {t : â„} (ht' : t * âˆ‘ i, (c i) ^ 2 â‰¤ 1) :
  (Î¼ (fun Ï‰ : Î© â†¦ (f âˆ˜ (Function.swap X)) Ï‰ - Î¼[f âˆ˜ (Function.swap X)] â‰¥ Îµ)).toReal â‰¤
    (-2 * Îµ ^ 2 * t).exp := by
  let m := Fintype.card Î¹
  let Î¹m : Î¹ â‰ƒ Fin m := Fintype.equivFinOfCardEq rfl
  let X' := X âˆ˜ Î¹m.symm
  let f' := f âˆ˜ (fun s â†¦ s âˆ˜ Î¹m)
  let c' := c âˆ˜ Î¹m.symm

  have hfÎ¹ (i : Fin m) (x : Fin m â†’ ğ“§) (x' : ğ“§) : |f' x - f' (Function.update x i x')| â‰¤ c' i := by
    dsimp [f']
    rw [Function.update_comp_equiv x Î¹m i x']
    apply hf

  have hf'' : StronglyMeasurable f' := by
    apply hf'.stronglyMeasurable.comp_measurable
    apply measurable_pi_iff.mpr
    intro i
    exact measurable_pi_apply (Î¹m i)

  have hIndep' : iIndepFun X' Î¼ := by
    apply ProbabilityTheory.iIndepFun.comp_right hX'
    exact Equiv.injective Î¹m.symm

  have hX'' := fun i â†¦ hX (Î¹m.symm i)

  have ht'' : t * âˆ‘ (i' : Fin m), c' i' ^2 â‰¤ 1 := by
    have : âˆ‘ (i' : Fin m), c' i' ^2 = âˆ‘ (i : Î¹), c i ^2 := by
      exact Fintype.sum_equiv Î¹m.symm (fun x â†¦ c' x ^ 2) (fun x â†¦ c x ^ 2) (congrFun rfl)
    rw [this]
    assumption
  if ht : t â‰¥ 0 then
    have := mcdiarmid_inequality_aux hX'' hIndep' hfÎ¹ hf'' hÎµ ht ht''
    have eq : (f' âˆ˜ Function.swap fun i â†¦ X (Î¹m.symm i)) = (f âˆ˜ Function.swap X) := by
      dsimp only [f']
      ext Ï‰
      simp only [Function.comp_apply]
      apply congrArg
      ext i
      dsimp [Function.swap]
      simp
    rw [eq] at this
    exact this
  else
    calc
      _ â‰¤ (1 : ENNReal).toReal := by
        apply ENNReal.toReal_mono
        exact ENNReal.one_ne_top
        apply MeasureTheory.prob_le_one
      _ = (1 : â„) := rfl
      _ â‰¤ _ := by
        apply Real.one_le_exp
        simp only [ge_iff_le, not_le] at ht
        apply mul_nonneg_of_nonpos_of_nonpos
        Â· norm_num
          exact sq_nonneg Îµ
        Â· exact le_of_lt ht


theorem mcdiarmid_inequality_neg
  (X : Î¹ â†’ Î© â†’ ğ“§) (hX : âˆ€ i, Measurable (X i))
  (hX' : iIndepFun X Î¼) (f : (Î¹ â†’ ğ“§) â†’ â„)
  (c : Î¹ â†’ â„)
  (hf : âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), |f x - f (Function.update x i x')| â‰¤ c i)
  (hf' : Measurable f)
  (Îµ : â„) (hÎµ : Îµ â‰¥ 0)
  (t : â„) (ht' : t * âˆ‘ i, (c i) ^ 2 â‰¤ 1):
  (Î¼ (fun Ï‰ : Î© â†¦ (f âˆ˜ (Function.swap X)) Ï‰ - Î¼[f âˆ˜ (Function.swap X)] â‰¤ -Îµ)).toReal â‰¤
    (-2 * Îµ ^ 2 * t).exp := by
  have hmf : âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), |(-f) x - (-f) (Function.update x i x')| â‰¤ c i := by
    intro i x x'
    rw [â†abs_neg]
    simp only [Pi.neg_apply, sub_neg_eq_add, neg_add_rev, neg_neg]
    rw [add_comm]
    apply hf
  have hmf' : Measurable (-f) := by
    apply Measurable.comp measurable_neg hf'
  have : (fun Ï‰ â†¦ (f âˆ˜ Function.swap X) Ï‰ - âˆ« (a : Î©), (f âˆ˜ Function.swap X) a âˆ‚Î¼ â‰¤ -Îµ)
    = (fun Ï‰ â†¦ ((-f) âˆ˜ Function.swap X) Ï‰ - âˆ« (a : Î©), ((-f) âˆ˜ Function.swap X) a âˆ‚Î¼ â‰¥ Îµ) := by
    ext Ï‰
    have : ((-f) âˆ˜ Function.swap X) Ï‰ - âˆ« (a : Î©), ((-f) âˆ˜ Function.swap X) a âˆ‚Î¼
      = -((f âˆ˜ Function.swap X) Ï‰ - âˆ« (a : Î©), (f âˆ˜ Function.swap X) a âˆ‚Î¼) := by
      simp only [Function.comp_apply, Pi.neg_apply, neg_sub]
      rw [integral_neg]
      ring_nf
    rw [this]
    exact le_neg
  rw [this]
  apply mcdiarmid_inequality_pos X hX hX' hmf hmf' hÎµ ht'

local notation "Î¼â¿" => Measure.pi (fun _ â†¦ Î¼)

theorem mcdiarmid_inequality_pos'
  {X' : Î© â†’ ğ“§} (hX'' : Measurable X')
  {f' : (Î¹ â†’ ğ“§) â†’ â„}
  {c' : Î¹ â†’ â„}
  (hfÎ¹ : âˆ€ (i : Î¹) (x : Î¹ â†’ ğ“§) (x' : ğ“§), |f' x - f' (Function.update x i x')| â‰¤ c' i)
  (hf'' : Measurable f')
  {Îµ : â„} (hÎµ : Îµ â‰¥ 0)
  {t : â„} (ht' : t * âˆ‘ i, (c' i) ^ 2 â‰¤ 1) :
  (Î¼â¿ (fun Ï‰ : Î¹ â†’ Î© â†¦ (f' (X' âˆ˜ Ï‰)) - Î¼â¿[fun Ï‰ : Î¹ â†’ Î© â†¦ f' (X' âˆ˜ Ï‰)] â‰¥ Îµ)).toReal â‰¤
    (-2 * Îµ ^ 2 * t).exp := by
  let X : Î¹ â†’ (Î¹ â†’ Î©) â†’ ğ“§ := fun i Ï‰ â†¦ X' (Ï‰ i)
  have hX : âˆ€ i, Measurable (X i) := fun i â†¦ by measurability
  have hX' : iIndepFun X Î¼â¿ := pi_comp_eval_iIndepFun hX''
  exact mcdiarmid_inequality_pos X hX hX' hfÎ¹ hf'' hÎµ ht'
